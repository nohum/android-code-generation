grammar at.fhj.gaar.androidapp.AppDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate appDsl "http://www.fhj.at/gaar/androidapp/AppDsl"

AndroidAppProject:
	(applications += Application) (',' applications += Application)*
;

terminal BOOLEAN returns ecore::EBoolean:
    'true' | 'false' | 'TRUE' | 'FALSE'
;

Application:
	'application' packageName = PackageName
	'{'
		// allow only one attribute or more than one (separated by ",")
		attributes += ApplicationAttribute (',' attributes += ApplicationAttribute)*
	'}'
;

ClassName:
	ID
;

PackageName:
	ID ('.' ID)*
;

ApplicationAttribute:
	ApplicationTitle |
	ApplicationMinSdk |
	ApplicationTargetSdk |
	ApplicationCompileSdk |
	ApplicationPermissionList |
	ApplicationElementList |
	ApplicationMainActivity
;

ApplicationTitle: 
	'title' title = STRING
;

ApplicationMinSdk:
	'min-sdk' minSdk = INT
;

ApplicationTargetSdk:
	'target-sdk' targetSdk = INT
;

ApplicationCompileSdk:
	'compile-sdk' compileSdk = INT
;

ApplicationPermissionList:
	'permissions'
	'['
		permissions += Permission (',' permissions += Permission)*
	']'
;

ApplicationElementList:
	'elements'
	'['
		elements += ApplicationElement (',' elements += ApplicationElement)*
	']'
;

ApplicationMainActivity:
	'main-activity' launcherActivity = STRING
;

Permission:
	PackageName // android permissions are in packagename fortmat (e.g. "android.permission.INTERNET")
;

ApplicationElement: 
	Activity | BroadcastReceiver | Service
;

Activity:
	'activity' className = ClassName
	'{'
		attributes += ActivityAttribute (',' attributes += ActivityAttribute)*
	'}'
;

BroadcastReceiver:
	'receiver' className = ClassName
	'{'
		attributes += BroadcastReceiverAttribute (',' attributes += BroadcastReceiverAttribute)*
	'}'
;

Service:
	'service' className = ClassName
	'{'
		attributes += ServiceAttribute (',' attributes += ServiceAttribute)*
	'}'
;

ActivityAttribute:
	ElementEnabledAttribute |
	ElementExportedAttribute
;

BroadcastReceiverAttribute:
	ElementEnabledAttribute |
	ElementExportedAttribute
;

ServiceAttribute:
	ElementEnabledAttribute |
	ElementExportedAttribute
;

ElementEnabledAttribute:
	'enabled' enabled = BOOLEAN
;

ElementExportedAttribute:
	'exported' exported = BOOLEAN
;
