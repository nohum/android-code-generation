grammar at.fhj.gaar.androidapp.AppDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate appDsl "http://www.fhj.at/gaar/androidapp/AppDsl"

AndroidAppProject:
	(applications += Application) (',' applications += Application)*
;

terminal BOOLEAN returns ecore::EBoolean:
    'true' |
    'false' |
    'TRUE' |
    'FALSE'
;

Application:
	'application' name = PackageName
	'{'
		// allow only one attribute or more than one (separated by ",")
		attributes += ApplicationAttribute (',' attributes += ApplicationAttribute)*
	'}'
;

PackageName:
	'"' ( name = ID ('.' ID)* ) '"'
;

ApplicationAttribute:
	ElementLabelAttribute |
	ApplicationMinSdk |
	ApplicationTargetSdk |
	ApplicationCompileSdk |
	ApplicationPermissionList |
	ApplicationElementList |
	ApplicationMainActivity
;

ApplicationMinSdk:
	'min-sdk' minSdk = INT
;

ApplicationTargetSdk:
	'target-sdk' targetSdk = INT
;

ApplicationCompileSdk:
	'compile-sdk' compileSdk = INT
;

ApplicationPermissionList:
	'permissions'
	'['
		// android permissions are in packagename format (e.g. "android.permission.INTERNET")
		permissions += PackageName (',' permissions += PackageName)*
	']'
;

ApplicationElementList:
	'elements'
	'['
		// TODO make empty list possible
		elements += ApplicationElement (',' elements += ApplicationElement)*
	']'
;

ApplicationMainActivity:
	'main-activity' launcherActivity = [Activity] // referenced by the magical "name" of an Activity
;

ApplicationElement: 
	Activity | BroadcastReceiver | Service
;

Activity:
	'activity' name = ID
	'{'
		// TODO make empty list possible
		attributes += ActivityAttribute (',' attributes += ActivityAttribute)*
	'}'
;

BroadcastReceiver:
	'receiver' name = ID
	'{'
		// TODO make empty list possible
		attributes += BroadcastReceiverAttribute (',' attributes += BroadcastReceiverAttribute)*
	'}'
;

Service:
	'service' name = ID
	'{'
		// TODO make empty list possible
		attributes += ServiceAttribute (',' attributes += ServiceAttribute)*
	'}'
;

ActivityAttribute:
	ElementEnabledAttribute |
	ElementExportedAttribute |
	ElementLabelAttribute |
	ElementIntentList |
	ActivityParentAttribute |
	ActivityLayoutAttribute
;

BroadcastReceiverAttribute:
	ElementEnabledAttribute |
	ElementExportedAttribute |
	ElementLabelAttribute |
	ElementIntentList |
	BroadcastReceiverActionAttribute
;

ServiceAttribute:
	ElementEnabledAttribute |
	ElementExportedAttribute |
	ElementLabelAttribute |
	ElementIntentList
;

ElementEnabledAttribute:
	'enabled' enabled = BOOLEAN
;

ElementExportedAttribute:
	'exported' exported = BOOLEAN
;

ElementLabelAttribute:
	'title' title = STRING
;

ElementIntentList:
	'intents'
	'['
		intents += PackageName (',' intents += PackageName)*
	']'
;

ActivityParentAttribute:
	'parent' parent = [Activity]
;

ActivityLayoutAttribute:
	'layout-elements'
	'['
		layoutElements += LayoutElement (',' layoutElements += LayoutElement)*
	']'
;

LayoutElement:
	Button | Spacer | Text
;

Button:
	'button' name = ID
	'{'
		// TODO make empty list possible
		attributes += ButtonAttribute (',' attributes += ButtonAttribute)*
	'}'
;

ButtonAttribute:
	ButtonLabelAttribute |
	ButtonActionAttribute
;

ButtonLabelAttribute:
	'title' title = STRING
;

ButtonActionAttribute:
	'action' action = LayoutElementClickAction
;

Spacer:
	'spacer'
;

Text:
	'text' text = STRING
;

LayoutElementClickAction:
	ActionShowToast |
	ActionStartActivity |
	ActionStartService
;

BroadcastReceiverActionAttribute:
	'action' action = BroadcastReceiverAction
;

BroadcastReceiverAction:
	ActionShowToast |
	ActionStartActivity |
	ActionStartService
;

ActionShowToast:
	'show-toast' toastText = STRING
;

ActionStartActivity:
	'start-activity' activity = [Activity]
;

ActionStartService:
	'start-service' service = [Service] // makes a reference to the "name" of an Service
;
